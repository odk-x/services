/**************************************************************************************************
                                        GRADLE CONFIG
 *************************************************************************************************/

apply plugin: 'jacoco'  // Can't (yet) use integrated Android JaCoCo for UnitTests,
                        // see: https://code.google.com/p/android/issues/detail?id=144664
apply plugin: 'com.android.application'

android {
    buildToolsVersion "22.0.1"
    compileSdkVersion 19
    defaultConfig {
        applicationId "org.opendatakit.core"
        minSdkVersion 16
        targetSdkVersion 16
    }

    buildTypes {
        debug {
            testCoverageEnabled true // Allows AndroidTest JaCoCo reports to be generated
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    packagingOptions {
        pickFirst 'META-INF/LICENSE'        /* File 1: jackson-databind-2.4.1.3.jar
                                               File 2: jackson-annotations-2.4.1.jar */

        pickFirst 'META-INF/LICENSE.txt'    /* File 1: commons-lang3-3.1.jar
                                               File 2: joda-time-2.2.jar */

        pickFirst 'META-INF/NOTICE'         /* File 1: jackson-databind-2.4.1.3.jar
                                               File 2: jackson-core-2.4.1.1.jar */

        pickFirst 'META-INF/NOTICE.txt'     /* File 1: commons-lang3-3.1.jar
                                               File 2: joda-time-2.2.jar */
    }
}

dependencies {
    compile fileTree(include: '*.jar', dir: 'libs')
    compile project(':androidlibrary:androidlibrary_lib')

    // Testing-only dependencies
    androidTestCompile 'com.android.support.test:runner:0.3'
    androidTestCompile 'com.android.support.test:rules:0.3'
    androidTestCompile 'com.android.support:support-annotations:22.2.0'
    androidTestCompile 'com.android.support:support-v13:22.2.0'
    androidTestCompile 'junit:junit:4.12'
    testCompile 'junit:junit:4.12'
}


/**************************************************************************************************
                                        CUSTOM TASKS
 *************************************************************************************************/

// Code to generate JaCoCo reports
task jacocoTestDebugReport(type:JacocoReport, dependsOn: ["jacocoMergeGen"]) {
    group = "Reporting"
    description = "Generate a JaCoCo code-coverage report for debug tests"

    classDirectories = fileTree( // Include relevant .class files
            dir: "${buildDir}/intermediates/classes/debug",
            excludes: ['**/R.class',
                       '**/R$*.class',
                       '**/*$ViewInjector*.*',
                       '**/BuildConfig.*',
                       '**/Manifest*.*',
                       '**/*Test*.class']
    )

    sourceDirectories = files("${buildDir.parent}/src/main/java") // Set src directories
    additionalSourceDirs = files([
            "${buildDir}/generated/source/buildConfig/debug",
            "${buildDir}/generated/source/r/debug"
    ])

    // Generate report from merged .exec file
    executionData = files("${buildDir}/jacoco/jacocoMergeGen.exec")

    reports { // Report output config
        xml {
            enabled = true
            destination "${project.buildDir}/reports/jacoco/jacoco.xml"
        }
        html {
            enabled = true
            destination "${project.buildDir}/reports/jacoco"
        }
    }
}

// Code to merge JaCoCo .exec/.ec files
// Outputs to ${buildDir}/jacoco/TASK_NAME.exec
task jacocoMergeGen(type: JacocoMerge, dependsOn: ["testDebug", "connectedAndroidTestDebug"]) {
    description = 'Merge test code-coverage results from all different test types'

    executionData = files([
            // Add any additional .exec/.ec files here if additional flavors of test need to be included
            "${buildDir}/jacoco/testDebug.exec", // UnitTest output
            "${buildDir}/outputs/code-coverage/connected/coverage.ec" // AndroidTest output
    ])
}